plugins {
    // id 'war'
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'net.nemerosa.versioning' version '2.8.2'
    id 'com.bmuschko.docker-remote-api' version '9.3.4'
}


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

def profile = parent.ext.get('profile')
def imageVersion = project.property('docker.image.version')
def imageVersionWithProfile = "$imageVersion-$profile"
def jarName = "$project.name-$imageVersion" + ".jar"
def dockerHost = project.property('docker.host')
def dockerRepo = project.property('docker.repo')
def dockerBuildDir = 'build/docker/'
def dockerWorkingDir = project.property('docker.work.dir') + "/twins"
def appName = 'twins'
def uniqueContainerName = "$dockerHost/$dockerRepo$appName:$imageVersionWithProfile"


println "Using profile [${profile}]"
project.version = imageVersion

repositories {
    mavenCentral()
    gradlePluginPortal()

}

docker {
    registryCredentials {
        url = "https://${dockerHost}/"
    }
}

task createDockerfile(type: Dockerfile) {
    dependsOn("bootJar")
    destFile.set(project.file("$dockerBuildDir/Dockerfile"))

    from("bellsoft/liberica-openjdk-alpine:20-x86_64")
    runCommand("mkdir -p $dockerWorkingDir")
    runCommand("mkdir $dockerWorkingDir/logs")
    workingDir(".$dockerWorkingDir")
    addFile("$jarName", "$dockerWorkingDir/twins.jar")
    entryPoint("java")
    defaultCommand("-jar", "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED", "-Dapp.home=$dockerWorkingDir", "$dockerWorkingDir/twins.jar")
    exposePort(8443)
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn("createDockerfile")
    doFirst {
        println("copy File begin")
        copy {
            from("build/libs")
            into(dockerBuildDir)
            include("*.jar")
        }
        println("copy File end")
    }
    inputDir.set(project.file(dockerBuildDir))
    images.add(uniqueContainerName)
}

task pushDockerImage(type: DockerBuildImage) {
    dependsOn("buildDockerImage")
    doLast {
        exec {
            executable("docker")
            args("push", "$uniqueContainerName")
        }
        println("Executed!")
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    implementation("org.springframework:spring-web:6.0.11")
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation("org.springframework.boot:spring-boot-starter-web:3.2.0") {
        exclude module: "spring-boot-starter-tomcat"
        exclude module: "spring-boot-starter-jetty"
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude module: "tomcat-embed-el"
        exclude group: "org.apache.tomcat"
    }
    implementation('org.springframework.boot:spring-boot-starter-undertow:3.2.0')
    implementation("org.springframework.amqp:spring-rabbit:3.1.0")
    implementation("org.springframework.amqp:spring-amqp:3.1.0")
    implementation("org.apache.httpcomponents:httpclient:4.5.14")
    implementation("org.apache.httpcomponents.client5:httpclient5:5.2.1")
    implementation("org.springframework.boot:spring-boot-starter-jdbc:3.2.0") {
        exclude group: "com.zaxxer", module: "HikariCP"
    }
    implementation("org.springframework.boot:spring-boot-starter-data-jpa:3.2.0")
    runtimeOnly("org.postgresql:postgresql:42.7.1")
    implementation('org.hibernate:hibernate-core:6.4.0.Final')
    implementation('io.hypersistence:hypersistence-utils-hibernate-62:3.6.1')
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0')
    implementation('org.springframework.boot:spring-boot-starter-validation:3.2.0')
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.8.0'
    implementation group: 'com.google.guava', name: 'guava', version: '32.1.3-jre'
}

configurations {
    runtime.exclude group: "com.zaxxer", module: "HikariCP"
}


jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': "org.twins.core.Application",
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision': versioning.info.commit,
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}